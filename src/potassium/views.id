use json;
use os;
use reflection;

use potassium.request;
use potassium.response;

class View {
    func run (self) {
        type = os.getEnv ("REQUEST_METHOD");
        cookiesEnv = os.getEnv ("HTTP_COOKIE");
        cookies = request.URLEncodedParameters (cookiesEnv).parameters;

        getParameters = null;
        getParameters = request.URLEncodedParameters(os.getEnv("QUERY_STRING"));
        postParameters = null;
        if (type == "POST") {
            contentType = os.getEnv("CONTENT_TYPE");
            if (contentType == "application/json") {
                data = stdin.read(Int(os.getEnv("CONTENT_LENGTH")));
                postParameters = request.Parameters(json.parse(data));
            }
            else if (contentType == "application/x-www-form-urlencoded") {
                data = stdin.read(Int(os.getEnv("CONTENT_LENGTH")));
                postParameters = request.URLEncodedParameters(data);
            }
        }
        requestData = request.Request(getParameters, postParameters, cookies);
        if (type == "GET" && reflection.hasAttribute (self, "get")) {
            print (self.get (requestData).getResponse ());
            return;
        }
        else if (type == "POST" && reflection.hasAttribute (self, "post")) {
            print (self.post (requestData).getResponse ());
            return;
        }

        print (self.methodNotAllowed ().getResponse ());
        return;
    }

    func methodNotAllowed (self) {
        return response.MethodNotAllowedResponse ();
    }
}
