use potassium.templates;
use potassium.db;
use potassium.auth;

class ResponseHeaders {
    func getStatusCode (self, code) {
        statusNames = HashMap();
        statusNames[200] = "OK";
        statusNames[302] = "Found";
        statusNames[400] = "Bad Request";
        statusNames[403] = "Forbidden";
        statusNames[404] = "Not Found";
        statusNames[405] = "Method Not Allowed";
        return statusNames[code];
    }
    
    func ResponseHeaders (self, statusCode, mime) {
        self.status = statusCode;
        self.mime = mime;
        self.headers = "";
    }
    
    func addHeader(self, header, value) {
        self.headers = self.headers + header + ": " + value + "\n";
    }
    
    func generate (self) {
        self.addHeader("Status", Str (self.status) + " " + 
                                 self.getStatusCode(self.status));
        if (self.mime != "") {
            self.addHeader("Content-type", self.mime);
        }
        self.headers = self.headers + "\n";
        return self.headers;
    }
}

class Response {
    func Response (self, response, statusCode, mime) {
        self.headers = ResponseHeaders(statusCode, mime);
        self.response = response;
    }
    
    func getResponse(self) {
        return self.headers.generate () + self.response;
    }
    
    func setAuthCookie (self, user, pass) {
        authHelper = auth.Auth (db.DatabaseSingleton.get ());
        tok = authHelper.createToken (user, pass);
        if (tok != null) {
            self.headers.addHeader ("Set-Cookie", "id={}".format (tok));
            return true;
        }
        return false;
    }
    
    func destroyAuthCookie (self, user) {
        authHelper = auth.Auth ();
        authHelper.destroyToken (user);
        self.headers.addHeader ("Set-Cookie", "id=;expires=expires="
                                + "Thu, 01 Jan 1970 00:00:00 UTC");
    }
}

class HTMLResponse : Response {
    func HTMLResponse (self, response, statusCode) {
        super(response, statusCode, "text/html");
    }
    
}

class TemplateResponse : HTMLResponse {
    func TemplateResponse (self, fileName, context) {
        super("<h1>Template Rendering Failed</h1>", 200);
        self.template = templates.Template (fileName);
        self.context = context;
    }
    
    func getResponse (self) {  
        return self.headers.generate () + self.template.render (self.context);
    }

    func setTemplate (self, fileName) {
        self.template = templates.Template (fileName);
    }
}

class RedirectResponse : Response {
    func RedirectResponse (self, uri) {
        super ("<a href=\"{}\">Redirect</a>".format (uri), 302, "text/html");
        self.headers.addHeader ("Location", "{}".format (uri));
    }
}

class MethodNotAllowedResponse : Response {
    func MethodNotAllowedResponse (self) {
        super ("<h1>Method not allowed!</h1>", 405, "text/html");
    }
}

class JSONResponse : Response {
    func JSONResponse (self, response, statusCode) {
        super(response, statusCode, "application/json");
    }
}

