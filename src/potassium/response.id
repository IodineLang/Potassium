use potassium.templates;

class ResponseHeaders {
    func getStatusCode (self, code) {
        statusNames = HashMap();
        statusNames[200] = "OK";
        statusNames[400] = "Bad Request";
        statusNames[403] = "Forbidden";
        statusNames[404] = "Not Found";
        return statusNames[code];
    }
    
    func ResponseHeaders (self, statusCode, mime) {
        self.status = statusCode;
        self.mime = mime;
        self.headers = "";
    }
    
    func addHeader(self, header, value) {
        self.headers = self.headers + header + ": " + value + "\n";
    }
    
    func generate (self) {
        self.addHeader("Status", Str (self.status) + " " + 
                                 self.getStatusCode(self.status));
        self.addHeader("Content-type", self.mime);
        self.headers = self.headers + "\n";
        return self.headers;
    }
}

class Response {
    func Response (self, response, statusCode, mime) {
        self.headers = ResponseHeaders(statusCode, mime).generate();
        self.response = response;
    }
    
    func getResponse(self) {
        return self.headers + self.response;
    }
}

class HTMLResponse : Response {
    func HTMLResponse (self, response, statusCode) {
        super(response, statusCode, "text/html");
    }
    
}

class TemplateResponse : HTMLResponse {
    func TemplateResponse (self, fileName, context) {
        super(templates.Template(fileName).render(context), 200);
    }
}

class JSONResponse : Response {
    func JSONResponse (self, response, statusCode) {
        super(response, statusCode, "application/json");
    }
}

